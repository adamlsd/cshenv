#########################################
# CSHENV Environment, core loading file #
#########################################

# CSHENV Debugging tests
if( ( $?CSHENV_DEBUG ) || ( $?CSHENV_VERBOSE_STARTUP ) ) then
	echo "Currently running tcshrc script"
endif

set path=( ${CSHENV_DIR}/bin ${path} )


# Figure out what kind of tcsh we are running.
if( $?tcsh ) then
	set tcsh_major=$tcsh:r:r
	set tcsh_minor=$tcsh:r:e
	set tcsh_patch=$tcsh:e
	set tcsh_features=$version:as/ /.:as/,/./

	if( $tcsh_minor < 12 ) then
		echo "We are on a very ancient TCSH!  There may be many problems..."
	else
		if( $tcsh_minor == 14 && $tcsh_patch < 2 ) then
			if( $?CSHENV_DEBUG ) then
				echo "We may not have ADAM's patches..."
			endif
		endif
	endif

else
	# As we develop support for C++sh, we'll need this
	if( !$?cxxsh ) then
		echo "What!  We're not even in TCSH or something compatible, like C++sh!!!"
	endif
endif

set cur_feature=$tcsh_features:e

# TCSH Extended features configuration
set color
set noding
set padhour


if( $?CSHENV_DEBUG ) then
	echo "TCSH Featureset probed..."
endif


#The history size for your shell can be set globally now
if( $?CSHENV_HISTORY_SIZE ) then
	set history=${CSHENV_HISTORY_SIZE}
endif

# this safe_source alias is incredibly delicate.  It drives the whole
# optional sourcing mechanism -- DO NOT MESS WITH IT!
alias safe_source 'eval "if ( -f \!:1 ) then \\
source \!:1 \\
endif"'

if( $?CSHENV_DEBUG ) then
	echo "TCSH safe-source alias constructed..."
endif


safe_source ${CSHENV_DIR}/tcshrc.conf

# Dispatch to find out what our OS is and any fixups
# This will figure out what system we're on and setup all
# system specific notions.  From the sourced file,
# tcshrc.system.???? will be called
if( $?CSHENV_DEBUG ) then
	echo "Trying to source tcshrc.system"
endif
source ${CSHENV_DIR}/tcshrc.system

#For GNU ls(1) {From coreutils} and tcsh's color ls-F
#Also, coming soon, my port of ls-F highlighting to FreeBSD's
#ls(1) -G facility
if( $?CSHENV_LINUX_SUCKS ) then
	setenv LS_COLORS 'no=00:fi=00:di=01;34:ln=01;35:pi=40;33:so=01;35:bd=40;33;01:cd=40;33;01:or=01;05;41;37:ex=00;31:*.tar=01;31:*.tgz=01;31:*.tbz=01;31:*.arj=01;31:*.taz=01;31:*.lzh=01;31:*.zip=01;31:*.z=01;31:*.Z=01;31:*.gz=01;31:*.bz2=01;31:*.deb=01;31:*.rpm=01;31:*.jpg=01;35:*.png=01;35:*.gif=01;35:*.bmp=01;35:*.ppm=01;35:*.tga=01;35:*.xbm=01;35:*.xpm=01;35:*.tif=01;35:*.png=01;35:*.mpg=01;35:*.avi=01;35:*.fli=01;35:*.gl=01;35:*.dl=01;35:*.cpp=32:*.cc=32:*.c=32:*.l=32:*.cxx=32:*.hpp=33:*.h=33:*.i=33:*.a=31:*.asm=31:*.bin=35:*.sec=36:*.ko=31'
else
	setenv LS_COLORS 'no=00:fi=00:di=01;34:ln=01;35:pi=40;33:so=01;35:do=01;35:bd=40;33;01:cd=40;33;01:or=01;05;41;37:ex=00;31:*.tar=01;31:*.tgz=01;31:*.tbz=01;31:*.arj=01;31:*.taz=01;31:*.lzh=01;31:*.zip=01;31:*.z=01;31:*.Z=01;31:*.gz=01;31:*.bz2=01;31:*.deb=01;31:*.rpm=01;31:*.jpg=01;35:*.png=01;35:*.gif=01;35:*.bmp=01;35:*.ppm=01;35:*.tga=01;35:*.xbm=01;35:*.xpm=01;35:*.tif=01;35:*.png=01;35:*.mpg=01;35:*.avi=01;35:*.fli=01;35:*.gl=01;35:*.dl=01;35:*.cpp=32:*.cc=32:*.c=32:*.l=32:*.m=32:*.i=33:*.cxx=32:*.h=33:*.hpp=33:*.a=31:*.asm=31:*.bin=35:*.sec=36:*.ko=31'
endif

#The LSCOLORS for *BSD is always working, so there's no need to versionify
#this.  The environment variable's presence is trivial.
#For FreeBSD's ls(1), and derived utilities {Like MacOS X/Darwin's ls(1)}
setenv LSCOLORS 'ExFxcxdxbxegedabagacEB'
setenv CLICOLOR_FORCE

if( $?CSHENV_DEBUG ) then
	echo "Trying to source tcshrc.${CSHENV_SYSTEM}"
endif
safe_source ${CSHENV_DIR}/tcshrc.${CSHENV_SYSTEM}

# Having figured out the system, then we can load specific aliases

if( $?CSHENV_DEBUG ) then
	echo "Trying to source tcshrc.aliases"
endif
source ${CSHENV_DIR}/tcshrc.aliases

if( $?CSHENV_DEBUG ) then
	echo "Trying to source tcshrc.${CSHENV_HOSTNAME}"
endif
safe_source ${CSHENV_DIR}/tcshrc.${CSHENV_HOSTNAME}

if( $?CSHENV_ORGANIZATION ) then
    safe_source ${CSHENV_DIR}/tcshrc.${CSHENV_ORGANIZATION}
endif

if( $?CSHENV_DEBUG ) then
	echo "Trying to source tcshrc.bindkeys"
endif
source ${CSHENV_DIR}/tcshrc.bindkeys

set path=( ${path} /usr/sbin /sbin )

# TCSHRC Prompt is the LAST thing we do, since it's the entry to
# user interface

if( $?CSHENV_DEBUG ) then
	echo "Trying to source tcshrc.prompt"
endif
source ${CSHENV_DIR}/tcshrc.prompt

###########################
# vim: ft=tcsh
###########################
