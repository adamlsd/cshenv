##########################
### PROMPT DEFINITIONS ###
##########################

if( $?CSHENV_DEBUG ) then
	echo "Sourcing prompt setup and definitions..."
endif

if( ! $?unknown_tty ) then
	set unknown_tty="?"
	if( $?CSHENV_DEBUG ) then
		echo "Unknown tty set..."
	endif
endif


set promptchars='%#'

set prompt_simple="${oldp_color_seq}[%n@%m: %c]%# "

# Screens are almost certainly not a million chars wide.
# If they are... oh well, that's a really weird one.
set date_prefix="%{${csi}A${csi}1000000G${csi}18D%}"
set date_suffix="%{${csi}G${csi}1B%}"
#set screen_return="%{8%}"
set date_prompt_core="${date_color_seq}%{%Y-%W-%D %}${time_color_seq}%{%P%}"
set date_prompt_left="${date_prompt_core}"
set date_prompt="${date_prefix}${date_prompt_core}${date_suffix}"
#unset edit

if( $?CSHENV_DEBUG ) then
	echo "Building Cool prompt"
endif


set depth_prompt=" ${depth_color_seq}(${shlvl})"
if( $?CSHENV_HIDE_SHELL_LEVEL_AT_ONE ) then
	if( $shlvl == 1 ) then
		set depth_prompt=""
	endif
endif

#we "convert" pts/$number entries to ttypK entries?
set line_prompt="${line_color_seq}`tty | sed -e 's;/dev/tty;;' -e 's;/dev/pts/;p/;' -e 's;/dev/;;' -e 's/not a tty/${unknown_tty}/'`"
set user_prompt="${name_color_seq}%n"
set host_prompt="${host_color_seq}${CSHENV_HOSTNAME}"
set path_prompt="${path_color_seq}%c02"
set end_prompt="${prompt_color_seq}%#${command_color_seq} "


set prompt_cool="${date_prompt}${text_color_seq}%L${brace_color_seq}\[${line_prompt}${depth_prompt}${text_color_seq}\- ${user_prompt}${at_color_seq}@${host_prompt}${text_color_seq}\: ${path_prompt}${brace_color_seq}\]${end_prompt}"

if( ! ${?cshenv_user_prompt_override} ) then
	set prompt="$prompt_cool"
endif
alias oldp 'set prompt="$prompt_simple"; set cshenv_user_prompt_override'
alias newp 'set prompt="$prompt_cool"; set cshenv_user_prompt_override'

###########################
# vim:filetype=tcsh
###########################
